set term pdfcairo enhanced font "Courier,17" size 21cm,29.7cm
set output "ethylene-ignition-history.pdf"
REV = system("git reflog -1 | awk '{print $1}'")
set label "[rev " . REV . "]" at screen 0.02,0.99 font ",17"

set multiplot layout 3,2 scale 0.95,0.95 title "ethylene/air ignition, p = 1bar"

set size square
set xtics 0.02
set xrange [0:0.06]
set ylabel "concentration, mol/cm^3"
set logscale y
set key top
set yrange [1e-15:1e-5]
set ytics 1e-15,1e5,1e-5
unset mytics 
set ytics add("" 1e-14 1, "" 1e-13 1, "" 1e-12 1, "" 1e-11 1, "" 1e-9 1, "" 1e-8 1, "" 1e-7 1, "" 1e-6 1)

set format y "10^{%L}"
set key bottom
plot "isobaric-reactor.data" u ($1*1000.0):3 t 'C_2H_4'  w l lw 3, \
     "" u ($1*1000.0):4 t 'C_2H_3' w l lw 3, \
     "" u ($1*1000.0):5 t 'HO_2' w l lw 3

plot "isobaric-reactor.data" u ($1*1000.0):6 t 'CH_3'  w l lw 3, \
     "" u ($1*1000.0):7 t 'C_2H_5' w l lw 3, \
     "" u ($1*1000.0):8 t 'C_2H_4O' w l lw 3

plot "isobaric-reactor.data" u ($1*1000.0):9 t 'CO'  w l lw 3, \
     "" u ($1*1000.0):10 t 'CHO' w l lw 3, \
     "" u ($1*1000.0):11 t 'CH_2O' w l lw 3

plot "isobaric-reactor.data" u ($1*1000.0):12 t 'H'  w l lw 3, \
     "" u ($1*1000.0):13 t 'OH' w l lw 3, \
     "" u ($1*1000.0):14 t 'O' w l lw 3, \
     "" u ($1*1000.0):15 t 'H_2O_2' w l lw 3	

set xlabel "time, ms"
set xtics 0.02
set xrange [0:0.06]

plot "isobaric-reactor.data" u ($1*1000.0):16 t 'H_2O'  w l lw 3, \
     "" u ($1*1000.0):17 t 'CH_2CHO' w l lw 3, \
     "" u ($1*1000.0):18 t 'CH_2CO' w l lw 3


unset logscale y
unset format y
set ylabel "T, K"
set yrange [1200:3000]
set ytics 200
plot "isobaric-reactor.data" u ($1*1000.0):2 notitle w l lw 3

unset multiplot


